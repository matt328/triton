#version 450
#extension GL_EXT_buffer_reference : enable
#extension GL_EXT_buffer_reference_uvec2 : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : enable

layout(local_size_x = 16, local_size_y = 1, local_size_z = 1) in;

struct InstanceData {
  mat4 modelMatrix;
  uint indexCount;
  uint instanceCount;
  uint vertexOffset;
  uint instanceID;
};

struct DrawCommand {
  uint vertexCount;
  uint instanceCount;
  uint firstVertex;
  uint firstInstance;
};

layout(push_constant) uniform PushConstants {
  uint64_t drawCommandBufferAddress;
  uint64_t instanceDataBufferAddress;
  uint instanceDataLength;
}
pc;

layout(buffer_reference, std430) buffer DrawCommandBuffer {
  DrawCommand commands[];
};

layout(buffer_reference, std430) buffer InstanceDataBuffer {
  InstanceData instanceData[];
};

void main() {
  uint index = gl_GlobalInvocationID.x;

  // Access the buffers via push constant device addresses
  DrawCommandBuffer drawCommands = DrawCommandBuffer(pc.drawCommandBufferAddress);
  InstanceDataBuffer instanceData = InstanceDataBuffer(pc.instanceDataBufferAddress);

  // Check bounds
  if (index >= pc.instanceDataLength) {
    return;
  }

  InstanceData data = instanceData.instanceData[index];

  // Populate indirect draw command
  drawCommands.commands[index].vertexCount = data.indexCount;
  drawCommands.commands[index].instanceCount = data.instanceCount;
  drawCommands.commands[index].firstVertex = data.vertexOffset;
  drawCommands.commands[index].firstInstance = data.instanceID;
}
